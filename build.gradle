plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.5.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

group 'com.github.grishberg.android'
version '22.07.07.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        url = uri("https://www.jetbrains.com/intellij-repository/snapshots/")
    }
    maven {
        url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
    }
    maven {
        url = uri("https://www.jetbrains.com/intellij-repository/releases")
    }
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://maven.google.com'
    }
}

if (!hasProperty('studioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    updateSinceUntilBuild true
    plugins 'android'
    version "213-EAP-SNAPSHOT"
}

runIde {
    ideDirectory studioCompilePath
}

dependencies {
    implementation "org.jooq:joor-java-8:0.9.7"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-swing:1.3.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.slf4j:slf4j-log4j12:1.7.25"
    compile group: 'log4j', name: 'log4j', version: '1.2.17'

    compileOnly fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    testCompile fileTree(dir: "$studioCompilePath/plugins/android/lib", include: ['*.jar'])
    testCompile fileTree(dir: "$studioCompilePath/lib", include: ['*.jar'])
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}


patchPluginXml {
    sinceBuild("211")
    changeNotes """
      Enable "Select parent view on next click" feature by default.<br>
      Force use protocol level 2.<br>
      """
}
